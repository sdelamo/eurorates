/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Copyright 2020 Sergio del Amo.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id 'org.kordamp.gradle.project' version '0.39.0'
    id 'org.kordamp.gradle.bintray' version '0.39.0'
    id 'groovy'
    id "com.gradle.build-scan" version "3.4.1"
}

ext {
    githubSlug = 'euro-foreign-exchange-reference-rates'
    githubOrg = 'sdelamo'
    bintrayUserOrg = 'groovycalamari'
}

if (System.getenv('BINTRAY_USER') != null) {
    ext.bintrayUsername = System.getenv('BINTRAY_USER')
} else if (!project.hasProperty('bintrayUsername')) {
    ext.bintrayUsername = '**UNDEFINED**'
}
if (System.getenv('BINTRAY_KEY') != null) {
    ext.bintrayApiKey = System.getenv('BINTRAY_KEY')
} else if (!project.hasProperty('bintrayApiKey')) {
    ext.bintrayApiKey   = '**UNDEFINED**'
}

config {
    release = (rootProject.findProperty('release') ?: false).toBoolean()

    info {
        name        = 'Euro foreign exchange reference rates API'
        vendor      = 'Groovy Calamari'
        description = 'Java Micronaut HTTP Client for Euro foreign exchange reference rates API'
        inceptionYear = '2020'

        links {
            website      = "https://github.com/${githubOrg}/${githubSlug}"
            issueTracker = "https://github.com/${githubOrg}/${githubSlug}/issues"
            scm          = "https://github.com/${githubOrg}/${githubSlug}.git"
        }
        people {
            person {
                id    = 'sdelamo'
                name  = 'Sergio del Amo'
                roles = ['developer']
            }
        }
        ciManagement {
            system = "Github Actions"
            url = "https://github.com/${githubOrg}/${githubSlug}/actions"
        }
    }

    licensing {
        licenses {
            license {
                id = 'Apache-2.0'
            }
        }
    }

    bintray {
        credentials {
            username = project.bintrayUsername
            password = project.bintrayApiKey
        }
        userOrg    = bintrayUserOrg
        name       = githubSlug
        githubRepo = "${githubOrg}/${githubSlug}"
        publish = true
        publicDownloadNumbers = true
    }
}

allprojects {
    apply plugin: 'idea'
    config {
        docs {
            groovydoc  { enabled = false }
        }
    }
    repositories {
        mavenCentral()
        maven { url "https://jcenter.bintray.com" }
    }
}
subprojects {
    compileGroovy {
        sourceCompatibility = project.sourceCompatibility
        targetCompatibility = project.targetCompatibility
    }

    config {
        coverage {
            jacoco {
                enabled = project.file('src/test').exists()
            }
        }
    }
}

buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
}